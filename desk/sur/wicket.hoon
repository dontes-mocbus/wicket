::  /sur/wicket: types for the %wicket authorization server
::
::    A wicket is a small door in a larger gate: Earth MCP clients enter
::    here; they do not own the ship. Resource desks copy this file.
::
|%
+$  scope     @t            ::  "recipe.read" — cords, not nested terms
+$  resource  @t            ::  absolute URL https://host/mcp/recipe
+$  client-id  @t
::
::  Consent text supplied by a resource server. An empty title or
::  label falls back to the URL or the scope cord.
::
::  care: %off is checked. %ask is unchecked. %ship is unchecked
::  and marked "Controls the ship". A bare loobean still works:
::  & is %ship and | is %off.
::
+$  care
  $?  %.n
      %.y
      %off
      %ask
      %ship
  ==
+$  scope-note
  $:  label=@t
      =care
  ==
+$  scope-copy
  $:  title=@t
      notes=(map scope scope-note)
  ==
::
+$  client
  $:  id=client-id
      name=@t
      redirect-uris=(set @t)
      created=@da
  ==
::
+$  registered-resource
  $:  url=resource
      agent=term
      scopes=(set scope)
      audience=@t
  ==
::
+$  authz
  $:  id=@uv
      client=client-id
      redirect=@t
      state=@t
      challenge=@t
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
  ==
::
+$  code
  $:  prefix=@t             ::  first 8 bytes of the code; the code is not stored
      client=client-id
      redirect=@t
      challenge=@t
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
      used=?
  ==
::
+$  access
  $:  prefix=@t             ::  first 8 bytes of the bearer; the bearer is not stored
      client=client-id
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
  ==
::
+$  refresh
  $:  prefix=@t
      client=client-id
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
  ==
::
+$  grant
  $:  client=client-id
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
  ==
::
+$  authorization
  $:  client=client-id
      resource=(unit resource)
      scopes=(list scope)
      updated=@da
  ==
::
+$  token-info
  $:  id-prefix=@t          ::  first 8 bytes; listings never include the bearer
      client=client-id
      resource=(unit resource)
      scopes=(list scope)
      expires=@da
  ==
::
+$  action
  $%  [%set-origin url=@t]     ::  public origin; becomes JSON "issuer"
      [%set-registration open=?]
      [%deny-address =address:eyre]
      [%allow-address =address:eyre]
      [%register-resource =registered-resource]
      [%set-scope-copy url=resource =scope-copy]
      [%unregister-resource url=resource]
      [%revoke-client id=client-id]
      [%revoke-access id=@t]
      [%revoke-refresh id=@t]
      $:  %drop-scopes
          client=client-id
          resource=(unit resource)
          scopes=(list scope)
      ==
  ==
--
